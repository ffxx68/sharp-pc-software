5 REM  R* ARE REAL PARTS AND I* ARE IMAGENARY PARTS OF THE SV
10 R0=1:I0=0:R1=0:I1=0
15 R2=0:I2=0:R3=0:I3=0
20 A0=0
25 SHOTS=28
40 PAUSE "SHARP-PC QUANTUM SIMULATOR"
45 PAUSE "CREATED BY DAVIDE GESSA (DAKK)"
50 INPUT "ENTER GATE SEQ (X0,X1,Y0,Y1,Z0,Z1,H0,H1,CX,SW)",G$
65 PAUSE "CALCULATING THE STATEVECTOR..."
70 FOR I=1 TO LEN (G$) STEP 2
80 GATE$= MID$ (G$,I,2)
90 GOSUB 200
100 NEXT I
102 SQ=R0*R0+I0*I0+R1*R1+I1*I1+R2*R2+I2*I2+R3*R3+I3*I3
103 IF ABS (SQ-1)>0.00001 THEN GOSUB 600
105 PAUSE "RUNNING ";SHOTS;" ITERATIONS..."
110 Z0=0:Z1=0:Z2=0:Z3=0
115 P0=(R0*R0+I0*I0)
120 P1=(R1*R1+I1*I1)+P0
125 P2=(R2*R2+I2*I2)+P1
130 P3=(R3*R3+I3*I3)+P2
135 FOR I=1 TO SHOTS
140 R= RND (0)
141 IF R<P0 THEN LET Z0=Z0+1
142 IF (R>=P0) AND (R<P1) THEN LET Z1=Z1+1
143 IF (R>=P1) AND (R<P2) THEN LET Z2=Z2+1
144 IF (R>=P2) AND (R<P3) THEN LET Z3=Z3+1
146 NEXT I
150 PAUSE "RESULTS:"
155 R$="00: "+ STR$ (Z0)+" ": FOR I=1 TO Z0:R$=R$+"*": NEXT I: PRINT R$
160 R$="10: "+ STR$ (Z1)+" ": FOR I=1 TO Z1:R$=R$+"*": NEXT I: PRINT R$
165 R$="01: "+ STR$ (Z2)+" ": FOR I=1 TO Z2:R$=R$+"*": NEXT I: PRINT R$
170 R$="11: "+ STR$ (Z3)+" ": FOR I=1 TO Z3:R$=R$+"*": NEXT I: PRINT R$
175 GOTO 1000
200 REM  SIMULATE GATE OPERATION
210 IF GATE$="X0" THEN GOSUB 400
220 IF GATE$="X1" THEN GOSUB 420
230 IF GATE$="Y0" THEN GOSUB 440
240 IF GATE$="Y1" THEN GOSUB 460
250 IF GATE$="Z0" THEN GOSUB 480
260 IF GATE$="Z1" THEN GOSUB 500
270 IF GATE$="H0" THEN GOSUB 520
280 IF GATE$="H1" THEN GOSUB 540
290 IF GATE$="CX" THEN GOSUB 560
300 IF GATE$="SW" THEN GOSUB 580
310 RETURN
400 REM  X0 GATE
410 A0=R0:R0=R1:R1=A0
411 A0=I0:I0=I1:I1=A0
412 A0=R2:R2=R3:R3=A0
413 A0=I2:I2=I3:I3=A0
416 RETURN
420 REM  X1 GATE
425 A0=R1:R1=R3:R3=A0
426 A0=I1:I1=I3:I3=A0
427 A0=R0:R0=R2:R2=A0
428 A0=I0:I0=I2:I2=A0
440 REM  Y0 GATE
446 A0=I0:I0=-R0:R0=A0
447 A0=I1:I1=-R1:R1=A0
448 A0=I2:I2=-R2:R2=A0
449 A0=I3:I3=-R3:R3=A0
450 RETURN
460 REM  Y1 GATE
466 A0=I1:I1=-R1:R1=A0
467 A0=I3:I3=-R3:R3=A0
468 RETURN
480 REM  Z0 GATE
482 I2=-I2:I3=-I3
483 RETURN
500 REM  Z1 GATE
502 I1=-I1:I3=-I3
503 RETURN
520 REM  H0 GATE
521 A0=(R0+R1)/ SQR (2):A1=(I0+I1)/ SQR (2)
522 B0=(R0-R1)/ SQR (2):B1=(I0-I1)/ SQR (2)
523 R0=A0:I0=A1:R1=B0:I1=B1
525 A0=(R2+R3)/ SQR (2):A1=(I2+I3)/ SQR (2)
526 B0=(R2-R3)/ SQR (2):B1=(I2-I3)/ SQR (2)
527 R2=A0:I2=A1:R3=B0:I3=B1
528 RETURN
540 REM  H1 GATE
541 A0=(R0+R2)/ SQR (2):A1=(I0+I2)/ SQR (2)
542 B0=(R0-R2)/ SQR (2):B1=(I0-I2)/ SQR (2)
543 R0=A0:I0=A1:R2=B0:I2=B1
545 A0=(R1+R3)/ SQR (2):A1=(I1+I3)/ SQR (2)
546 B0=(R1-R3)/ SQR (2):B1=(I1-I3)/ SQR (2)
547 R1=A0:I1=A1:R3=B0:I3=B1
548 RETURN
560 REM  CX GATE
561 A0=R1:R1=R3:R3=A0
562 A0=I1:I1=I3:I3=A0
579 RETURN
580 REM  SW GATE
581 A0=R1:R1=R2:R2=A0
582 A0=I1:I1=I2:I2=A0
590 RETURN
600 REM  STATEVCECTOR NORMALIZATION
601 NF= SQR (1/SQ)
602 R0=R0*NF
603 I0=I0*NF
604 R1=R1*NF
605 I1=I1*NF
606 R2=R2*NF
607 I2=I2*NF
608 R3=R3*NF
609 I3=I3*NF
610 RETURN
1000 PAUSE "END"
