5 "A": CLEAR: RESTORE 20
7  REM PC-1403 MATRIX CALCULATION EXAMPLE
10 DIM X(2,2): FOR I=0 TO 2: FOR J=0 TO 2: READ X(I,J): NEXT J: NEXT I
20 DATA 2,5,-1,1,-1,4,3,2,1
30 DIM Y(2,0): FOR I=0 TO 2: READ Y(I,0): NEXT I
40 DATA -1,12,9
41 OPEN "X:X.DAT" FOR OUTPUT AS #2
42 PRINT #2,X(*)
43 CLOSE
44 OPEN "X:Y.DAT" FOR OUTPUT AS #3
45 PRINT #3,Y(*)
46 CLOSE
50 CALL 26132: REM invX->X
60 CALL 26119: REM X*Y->X
70 FOR I=0 TO 2: PRINT X(I,0): NEXT I
80 END
101 REM PC-1403 ROM matrix subroutines
102 REM 26109 X+Y->X
103 REM 26114 X-Y->X
104 REM 26119 X*Y->X
105 REM 26132 invX->X
106 REM 26140 (x)+X->X
107 REM 26148 (x)-X->X
108 REM 26153 (x)*X->X
109 REM 26158 (x)*invX->X
110 REM 26163 X<>Y
111 REM 26171 trnX->X
112 REM 26176 detX->(x)
113 REM 26181 -X->X
114 REM 26186 squX->X
115 REM 26191 X->M
116 REM 26199 M->X
117 REM 26204 X+M->X
118 REM X, Y and M are matrices (DIMensioned 2-dimensional arrays) 
119 REM (x) is the simple BASIC variable X. 
