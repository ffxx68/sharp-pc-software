1 REM Pocket-Quest Game for Sharp PC-1403
2 REM (c) 2012-2022 by N. Haedecke 
10 PAUSE "-=POCKET-QUEST=-"
20 DIM M(16,4),M$(16,4),O(4),O$(4)*16,C$(1)*16,V$(3)*16,T$(1)*16,X$(1)*16,E$(1)*16
30 R=1:T=0:X=0:C$(1)=""
40 Z=1:L=1:P=6:D=1:S=0:U=0
50 RANDOM 
60 FOR I=1 TO 16: FOR J=1 TO 4
70 READ M(I,J)
80 NEXT J: NEXT I
90 FOR I=1 TO 16: FOR J=1 TO 4
100 READ M$(I,J)
110 NEXT J: NEXT I
120 FOR A=1 TO 4: READ O(A): NEXT A
130 W=RND 15: IF W<11 OR W=13 THEN GOTO 130
140 O(3)=W
150 FOR A=1 TO 4: READ O$(A): NEXT A
160 FOR A=1 TO 3: READ V$(A): NEXT A
170 REM MAIN LOOP
180 GOSUB 300
190 GOSUB 390
200 IF R=4 AND Z=1 THEN GOSUB 710
210 IF R=5 AND Z=1 THEN GOSUB 710
220 IF R=7 THEN LET P=6
230 IF R>9 AND R<90 AND R<>13 AND D=1 AND Z=1 THEN LET G=RND 10: IF G>5 THEN GOSUB 710
231 IF R=13 AND D=1 THEN GOSUB 710
240 IF R=9 AND D=1 AND Z=0 THEN LET Z=1
250 IF R=95 THEN PAUSE "* GAME OVER!": GOTO 270
260 IF R<>99 THEN GOTO 170
270 PAUSE "* MOVES: ";U
280 PAUSE "* SCORE: ";S
290 PAUSE "* GOOD BYE *": END 
300 REM DISPLAY ROOM
310 FOR A=1 TO 4: PAUSE M$(R,A): NEXT A
320 REM LIST OBJECTS
330 FOR A=1 TO 4
340 IF O(A)=R THEN PAUSE "YOU SEE: ";O$(A)
350 NEXT A
360 REM PROMPT
370 C$(1)="": INPUT "WHAT NOW? ";C$(1)
380 LET U=U+1: RETURN 
390 REM PARSER
400 X$(1)="":T$(1)="":T=0:X=0
410 IF LEN (C$(1))=1 THEN GOTO 620
420 IF C$(1)="QUIT" THEN LET R=99: RETURN 
430 IF C$(1)="INV" THEN GOTO 670
440 FOR A=1 TO LEN (C$(1))
450 IF MID$ (C$(1),A,1)=" " THEN LET T$(1)=LEFT$ (C$(1),A-1)
460 IF MID$ (C$(1),A,1)=" " THEN LET X$(1)=MID$ (C$(1),A+1,LEN (C$(1)))
470 NEXT A
480 FOR B=1 TO 3
490 IF T$(1)=V$(B) THEN LET T=B
500 NEXT B
510 FOR A=1 TO 4
520 IF X$(1)=O$(A) THEN LET X=A
530 NEXT A
540 IF T=1 AND O(X)=R THEN LET O(X)=-1: PAUSE "* TAKEN": RETURN 
550 IF T=2 AND O(X)=-1 THEN LET O(X)=R: PAUSE "* DROPPED": RETURN 
560 IF T=3 AND O(X)=-1 AND R=8 THEN LET R=9: LET O(X)=99: LET S=S+5: RETURN 
570 IF T=3 AND O(2)=99 AND R=8 THEN LET R=9: LET O(2)=99: RETURN 
580 IF T=3 AND O(2)=99 AND R=9 THEN LET R=8
585 IF T=3 AND O(3)=-1 THEN LET P=P+10: LET O(3)=99: PAUSE "* YOU FEEL STRONG": RETURN 
590 IF R=8 AND O(2)=99 THEN LET M$(8,3)="A ROPE LEAD DOWN"
600 IF R=8 AND O(2)=99 THEN LET M$(8,4)="TO A CAVE.": RETURN 
610 PAUSE "PARDON?": RETURN 
620 IF C$(1)="N" AND M(R,1)<>0 THEN LET R=M(R,1): RETURN 
630 IF C$(1)="S" AND M(R,2)<>0 THEN LET R=M(R,2): RETURN 
640 IF C$(1)="E" AND M(R,3)<>0 THEN LET R=M(R,3): RETURN 
650 IF C$(1)="W" AND M(R,4)<>0 THEN LET R=M(R,4): RETURN 
651 IF C$(1)="W" AND R=3 AND O(4)=-1 THEN GOSUB 660: GOTO 270
655 PAUSE "* NOT AN EXIT!": RETURN 
660 PAUSE "YOU ENTER THE"
661 PAUSE "TOWNHALL AND"
662 PAUSE "REPORT YOUR"
663 PAUSE "HEROIC QUEST"
664 PAUSE "AS YOU HAND"
665 PAUSE "OVER THE"
666 PAUSE "SACRED AMULET"
667 PAUSE "TO THE MAYOR."
668 PAUSE "WELL DONE,"
669 PAUSE "ADVENTURER!": RETURN 
670 REM INVENTORY
680 FOR A=1 TO 4
690 IF O(A)=-1 THEN PAUSE "<";O$(A);">"
700 NEXT A: PAUSE "PLAYER HP: ";P: RETURN 
710 REM FIGHT
720 IF L=1 AND R<>13 THEN LET Q=RND 5
721 IF L=1 AND R=13 THEN LET Q=RND 10
730 IF L=1 THEN LET F=Q:L=0
740 H=RND 2:K=RND 3: IF O(1)=-1 THEN LET H=H+1
745 IF R=4 THEN LET E$(1)="A THIEF ATTACKS"
750 IF R<9 AND R<>4 THEN LET E$(1)="A BEAR ATTACKS"
755 IF R>9 AND R<>13 AND R<>16 THEN LET E$(1)="A SPIDER ATTACKS"
760 IF R=13 THEN LET E$(1)="A DRAGON ATTACKS"
765 PAUSE E$(1)
770 PAUSE "YOU FIGHT BACK!"
780 P=P-K:Q=Q-H
790 PAUSE "PLAYER HP: "ðŸ˜œ
800 IF P<=0 THEN LET R=95: RETURN 
810 IF Q<=0 THEN PAUSE "* ENEMY KILLED !"
820 IF Q<=0 THEN LET S=S+(F*10)
830 IF Q<=0 AND O(2)=-2 AND R=4 THEN LET O(2)=R
840 IF Q<=0 AND L=0 THEN LET L=1
850 IF Q<=0 AND R=4 THEN LET Z=0
851 IF Q<=0 AND R=13 AND D=1 THEN LET D=0: LET Z=0
852 IF D=0 AND R=13 THEN LET O(4)=R
853 IF D=0 THEN LET Z=0
860 RETURN 
870 REM MAP DATA
871 DATA 0,2,0,0,1,3,5,4
872 DATA 2,7,6,0,0,0,2,0
873 DATA 16,0,0,2,0,0,0,3
874 DATA 3,0,0,0,0,0,0,16
875 DATA 10,0,0,0,15,9,11,14
876 DATA 14,15,12,10,13,15,14,11
877 DATA 0,12,0,0,12,11,15,10
878 DATA 11,10,12,14,0,5,8,0
900 REM ROOM DATA
901 DATA "UPON A HILL. A","PATH LEADS TO"
902 DATA "A SMALL TOWN IN","THE SOUTH."
903 DATA "IN A FORREST.","ON A CLEARING"
904 DATA "YOU REACH A","CROSS JUNCTION."
905 DATA "IN THE TOWN.","SOUTH: TEMPLE"
906 DATA "WEST: TOWNHALL","EAST: ARMORY"
907 DATA "LEAVING THE DARK","WOODS, THE PATH"
908 DATA "ENDS AT A RIVER.","ONLY EXIT: EAST."
909 DATA "YOU REACH THE","MOUNTAIN REGION."
910 DATA "THE PATH LEADS","NORTH AND WEST."
911 DATA "IN THE AMORY.","THE BLACKSMITH"
912 DATA "IS BUSY WITH","HIS IRON HAMMER."
913 DATA "AS YOU ENTER,","YOU FEEL A BOOST"
914 DATA "OF POWER RUSH","THROUGH YOU."
915 DATA "AT THE MOUNTAIN","TOP. YOU CAN SEE"
916 DATA "THE ENTRANCE TO","A CAVE BELOW."
917 DATA "IN THE CAVE. A","TUNNEL LEADS TO"
918 DATA "TO THE NORTH. A","ROPE LEADS UP."
919 DATA "YOU ARE IN A","NARROW TUNNEL."
920 DATA "EXITS ARE:","N,S,E,W"
921 DATA "YOU ARE IN A","NARROW TUNNEL."
922 DATA "EXITS ARE:","N,S,E,W"
923 DATA "YOU ARE IN A","NARROW TUNNEL."
924 DATA "EXITS ARE:","N,S,E,W"
925 DATA "YOU ARE IN A","NARROW TUNNEL."
926 DATA "EXITS ARE:","S"
927 DATA "YOU ARE IN A","NARROW TUNNEL."
928 DATA "EXITS ARE:","N,S,E,W"
929 DATA "YOU ARE IN A","NARROW TUNNEL."
930 DATA "EXITS ARE:","N,S,E,W"
931 DATA "YOU WALK ON","A ROCKY PATH."
932 DATA "EXITS LEAD","SOUTH AND EAST."
950 REM OBJECTS
951 DATA 6,-2,-2,-2
952 DATA "KNIFE","ROPE","POTION","AMULET"
960 REM VERBS
961 DATA "TAKE","DROP","USE"
