40 PAUSE "CELESTIAL NAVIGATION"
70 INPUT "YEAR (1980-2099)";YR
80 INPUT "MONTH (1-12)";MO
90 INPUT "DAY (1-31)";DY
100 INPUT "HOUR UT (0-23)";HR
110 INPUT "MINUTE UT (0-59)";MN
130 INPUT "YOUR LATITUDE (+ N, - S)";LT
140 INPUT "YOUR LONGITUDE (+ E, - W)";LG
160 INPUT "OBSERVED ALTITUDE (DEG)";HD
170 INPUT "OBSERVED ALTITUDE (MIN)";HM
180 HO=HD+HM/60
200 REM CALCULATE JULIAN DATE
210 A=INT((14-MO)/12)
220 Y=YR+4800-A
230 M=MO+12*A-3
240 JD=DY+INT((153*M+2)/5)+365*Y
250 JD=JD+INT(Y/4)-INT(Y/100)+INT(Y/400)-32045
260 REM ADD TIME FRACTION
270 T=(JD+(HR+MN/60)/24-2451545)/36525
280 REM SUN MEAN LONGITUDE
290 L0=280.46646+36000.76983*T+.0003032*T*T
300 L0=L0-INT(L0/360)*360
310 IF L0<0 THEN L0=L0+360
320 REM SUN MEAN ANOMALY
330 MA=357.52911+35999.05029*T-.0001537*T*T
340 MA=MA-INT(MA/360)*360
350 IF MA<0 THEN MA=MA+360
360 REM EQUATION OF CENTER
370 C=(1.914602-.004817*T-.000014*T*T)*SIN(MA)
380 C=C+(.019993-.000101*T)*SIN(2*MA)
390 C=C+.000289*SIN(3*MA)
400 REM TRUE LONGITUDE
410 TL=L0+C
420 REM NUTATION
430 OM=125.04-1934.136*T
440 LA=TL-.00569-.00478*SIN(OM)
450 REM OBLIQUITY
460 EP=23.439291-.0130042*T
470 EP=EP+.00256*COS(OM)
480 REM RIGHT ASCENSION
485 S1=SIN(LA)
486 C1=COS(LA)
487 IF ABS(C1)<.01 THEN C1=SGN(C1)*.011
490 RA=ATN(COS(EP)*S1/C1)
500 IF C1<0 THEN RA=RA+180
510 IF RA<0 THEN RA=RA+360
520 REM GREENWICH MEAN SIDEREAL TIME
530 GM=280.46061837+360.98564736629*(JD+(HR+MN/60)/24-2451545)
540 GM=GM+.000387933*T*T-T*T*T/38710000
550 GM=GM-INT(GM/360)*360
560 IF GM<0 THEN GM=GM+360
570 REM GREENWICH HOUR ANGLE
580 GH=GM-RA
590 IF GH<0 THEN GH=GH+360
600 REM CALCULATE DECLINATION
610 DC=ATN(SIN(EP)*SIN(LA)/SQR(1-(SIN(EP)*SIN(LA))^2))
620 REM CALCULATE LOCAL HOUR ANGLE
630 LH=GH-LG
640 IF LH<0 THEN LH=LH+360
650 IF LH>=360 THEN LH=LH-360
660 REM SIGHT REDUCTION - CALCULATE Hc
670 L2=LH
680 SS=SIN(LT)*SIN(DC)+COS(LT)*COS(DC)*COS(LH)
690 HC=ATN(SS/SQR(1-SS*SS))
700 REM CALCULATE AZIMUTH
710 CZ=(SIN(DC)-SIN(LT)*SS)/(COS(LT)*COS(HC))
720 IF CZ>1 THEN CZ=1
730 IF CZ<-1 THEN CZ=-1
740 ZN=ATN(CZ/SQR(1-CZ*CZ))
750 IF ZN<0 THEN ZN=ZN+180
760 IF SIN(LH)<0 THEN ZN=360-ZN
770 REM INTERCEPT
780 I=HO-HC
860 REM GHA
870 GD=INT(GH)
880 GM=INT((GH-GD)*60)
890 GS=INT(((GH-GD)*60-GM)*60)
900 X$="SUN GHA: "+STR$(GD)+"d "
910 Z$="":IF GM<10 THEN Z$="0"
920 X$=X$+Z$+STR$(GM)+"' "
930 Z$="":IF GS<10 THEN Z$="0"
940 X$=X$+STR$(GS)+CHR$(34)
945 PRINT X$
950 REM DECLINATION
960 DD=INT(ABS(DC))
970 DM=INT((ABS(DC)-DD)*60)
980 DS=INT(((ABS(DC)-DD)*60-DM)*60)
990 X$="SUN DC: "
1000 X$=X$+"S ":IF DC>=0 THEN X$="SUN DC: N "
1020 X$=X$+STR$(DD)+"d "
1030 Z$="":IF DM<10 THEN Z$="0"
1040 X$=X$+Z$+STR$(DM)+"' "
1050 Z$="":IF DS<10 THEN Z$="0"
1060 X$=X$+Z$+STR$(DS)+CHR$(34)
1065 PRINT X$
1080 REM LHA
1090 LD=INT(L2)
1100 LM=INT((L2-LD)*60)
1105 Z$="":IF LD<10 THEN Z$="0"
1110 X$=Z$+STR$(LD)+"d "
1120 Z$="":IF LM<10 THEN Z$="0"
1130 X$=X$+Z$+STR$(LM)+"'"
1135 PRINT "LHA: "+X$
1180 REM FORMAT Hc
1190 H1=INT(HC)
1200 H2=INT((HC-H1)*60)
1210 HS=INT(((HC-H1)*60-H2)*60)
1220 X$=STR$(H1)+"d "
1230 Z$="":IF H2<10 THEN Z$="0"
1240 X$=X$+Z$+STR$(H2)+"' "
1250 Z$="":IF HS<10 THEN Z$="0"
1260 X$=X$+Z$+STR$(HS)+CHR$(34)
1265 PRINT "Hc (COMPUTED): "+X$
1270 REM FORMAT Ho
1280 X$=STR$(HD)+"d "
1290 Z$="":IF HM<10 THEN Z$="0"
1300 X$=X$+Z$+STR$(HM)+"'"
1305 PRINT "Ho (OBSERVED): "+X$
1320 REM FORMAT INTERCEPT
1330 ID=INT(ABS(I))
1340 IM=INT((ABS(I)-ID)*60)
1350 X$=STR$(ID)+"' "
1360 IF IM>0 THEN X$=X$+STR$(IM)+CHR$(34)+" "
1370 X$=X$+"TOWARD":IF I<0 THEN X$=STR$(ID)+"' AWAY"
1375 IF IM>0 AND I<0 THEN X$=STR$(ID)+"' "+STR$(IM)+CHR$(34)+" AWAY"
1380 IF I=0 THEN X$=STR$(ID)+"' ON"
1385 PRINT "INTERCEPT: ";X$
1410 REM FORMAT AZIMUTH
1420 ZD=INT(ZN)
1430 ZM=INT((ZN-ZD)*60)
1440 X$=STR$(ZD)+"d "
1450 Z$="":IF ZM<10 THEN Z$="0"
1460 X$=X$+Z$+STR$(ZM)+"' T"
1465 PRINT "AZIMUTH: "+X$
1490 X$=STR$(ZD)+"d FOR "+STR$(ID)+"' TOWARD SUN"
1500 IF I<0 THEN X$=STR$(ZD)+"d FOR "+STR$(ID)+"' AWAY FROM SUN"
1510 IF I=0 THEN X$=STR$(ZD)+"d EXACTLY ON LOP"
1515 PRINT "BEARING "+X$
1540 INPUT "ANOTHER SIGHT (Y/N)";A$
1550 IF A$="Y" OR A$="y" THEN GOTO 70
1560 END
