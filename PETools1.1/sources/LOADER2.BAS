19999 END
20000 REM CODE BLOCKS MUST BE ON 5000,10000,15000
20020 IF CL$="NOCHK" THEN 20100
20030 GOSUB 20060
20040 GOSUB 24000:REM CHECK INTEGRITY
20050 GOTO 20110
20060 CLEAR:V$="1.10":DIM BA$(0)*64:J=0:GOSUB 5000:S=A,E=A+B,CS=C
20065 GOSUB 10000:E=E+B,CS=CS+C:GOSUB 15000:E=E+B-1,CS=CS+C
20070 GOSUB 21000:REM INTRO
20080 GOSUB 22000:REM CHECK HARDWARE
20090 RETURN
20100 GOSUB 20060
20110 GOSUB 25000:REM LOAD ML PROGRAM
20120 GOSUB 26000:REM MOVE POINTERS AND MAKE A NEW
20130 END
20999 REM INTRO
21000 PAUSE "This program will in-"
21010 PAUSE "stall PETOOLS ";V$;" on"
21020 PAUSE "your pocket computer."
21030 PAUSE "Caution: This procedure"
21040 PAUSE "will clear all memory"
21050 PAUSE "of your pocket computer"
21060 INPUT "Continue ? ";D$
21070 D$=LEFT$(D$,1):IF D$="Y" OR D$="y" RETURN
21080 IF D$="N" OR D$="n" END
21090 GOTO 21060
21999 REM CHECK HARDWARE
22000 PAUSE "Checking hardware ...":CALL &4B8
22010 REM CHECK FOR DOUBLE ADDRESS DECODING --> 8K MEMORY
22020 N=S+100:LM=1
22030 FOR I=S TO N
22040 A=PEEK I,B=PEEK(I+&6000)
22050 IF A<>B LET LM=0,I=N
22060 NEXT I
22070 REM CHECK START OF BASIC POINTER
22080 A=&8030:IF LM LET A=&E030
22090 B=256*PEEK &FF02+PEEK &FF01
22100 IF A=B THEN 22200
22110 PAUSE "Caution: The basic start"
22120 PAUSE "pointer does not match"
22130 PAUSE "the default setting."
22140 PAUSE "Maybe a machine language"
22150 PAUSE "program is loaded or you"
22160 PAUSE "aren't using a 1403(H)."
22170 INPUT "Continue anyway ? ";D$
22180 D$=LEFT$(D$,1):IF D$="N" OR D$="n" END
22190 IF D$<>"Y" AND D$<>"y" THEN 22170
22200 D$="1403H":IF LM LET D$="1403"
22210 PAUSE "You are using a PC-";D$:CALL &4B8
22220 RETURN
23999 REM CHECK INTEGRITY
24000 WAIT 0:RESTORE 5000
24010 A=0:PAUSE "Checking file integrity"
24020 P=0:GOSUB 30000:REM PROGRESS BAR 1
24030 FOR I=S TO E
24040 GOSUB 27000:A=A+B
24050 R=I-S,P=INT (15*R/(E-S))
24060 IF P=Q THEN 24090
24070 GOSUB 30000:REM PROGRESS BAR 1
24080 Q=P
24090 NEXT I:BEEP 1
24100 IF A=CS RETURN
24110 PAUSE "Data Error: if you typed"
24120 PAUSE "the Program, check for"
24130 PAUSE "typo's, if you loaded"
24140 WAIT:PRINT "it, try loading again."
24150 END
24999 REM LOAD ML PROGRAM
25000 WAIT 0:RESTORE 5000
25010 PAUSE "Loading PETOOLS routines"
25020 P=0:GOSUB 30100:REM PROGRESS BAR 2
25030 FOR I=S TO E
25040 GOSUB 27000:POKE I,B
25050 R=I-S,P=INT (15*R/(E-S))
25060 IF P=Q THEN 25090
25070 GOSUB 30100:REM PROGRESS BAR 2
25080 Q=P
25090 NEXT I:BEEP 1
25100 RETURN
25999 REM MOVE POINTERS AND MAKE A NEW
26000 PAUSE "Execute NEW ...":CALL &4B8
26010 A=E+1:IF LM LET A=A+&6000
26020 REM MOVE START OF BASIC POINTER
26030 POKE &FF01,A-INT (A/256)*256,INT (A/256)
26040 REM SWITCH TO PRO MODE AND EXECUTE NEW
26050 POKE 12349,16:NEW
26060 END
26998 REM THE READ ROUTINE
26999 REM USED VARS: BA$,J,K,L RETVAL: B
27000 IF J<=0 READ BA$(0):J=LEN BA$(0):K=1
27010 L=ASC MID$(BA$(0),K,1)
27020 IF L<58 LET L=L-48
27030 IF L>96 LET L=L-87
27040 B=16*L:L=ASC MID$(BA$(0),K+1,1)
27050 IF L<58 LET L=L-48
27060 IF L>96 LET L=L-87
27070 B=L+B:K=K+2:J=J-2:RETURN
29999 REM PROGRESS BAR 1
30000 Q=15-P
30010 PRINT "Checking ";LEFT$("ooooooooooooooo",P);LEFT$("...............",Q)
30020 CALL &4B8:RETURN
30099 REM PROGRESS BAR 2
30100 Q=15-P
30110 PRINT "Loading  ";LEFT$("OOOOOOOOOOOOOOO",P);LEFT$("ooooooooooooooo",Q)
30120 CALL &4B8:RETURN
